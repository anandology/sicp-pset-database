% for yTeX
% Copyright (c) 1990 Massachusetts Institute of Technology
% 
% This material was developed by the Scheme project at the Massachusetts
% Institute of Technology, Department of Electrical Engineering and
% Computer Science.  Permission to copy this material, to redistribute
% it, and to use it for any non-commercial purpose is granted, subject
% to the following restrictions and understandings.
% 
% 1. Any copy made of this material must include this copyright notice
% in full.
% 
% 2. Users of this material agree to make their best efforts (a) to
% return to the MIT Scheme project any improvements or extensions that
% they make, so that these may be included in future releases; and (b)
% to inform MIT of noteworthy uses of this material.
% 
% 3. All materials developed as a consequence of the use of this
% material shall duly acknowledge such use, in accordance with the usual
% standards of acknowledging credit in academic research.
% 
% 4. MIT has made no warrantee or representation that this material
% (including the operation of software contained therein) will be
% error-free, and MIT is under no obligation to provide any services, by
% way of maintenance, update, or otherwise.
% 
% 5. In conjunction with products arising from the use of this material,
% there shall be no use of the name of the Massachusetts Institute of
% Technology nor of any adaptation thereof in any advertising,
% promotional, or sales literature without prior written consent from
% MIT in each case. 


\typesize=11pt
\hsize=34pc
\vsize=50pc
\parskip 6pt plus 2pt
\def\v#1{\hbox{\bf #1}}
\def\unit#1{{\v{\^#1}}}

\def\psetheader{
\centerline{MASSACHUSETTS INSTITUTE OF TECHNOLOGY}
\centerline{Department of Electrical Engineering and Computer Science}
\centerline{6.001 Structure and Interpretation of Computer Programs}
\centerline{Fall Semester, 1987}}

\def\code#1{\beginlisp
#1
\endlisp

\vskip .1in}

\rectoleftheader={6.001 -- Fall Semester 1987}
\rectorightheader={Problem Set 3}
\onheaders
\onfooters

\null
\vskip 1truein

\psetheader

\vskip .25truein

\centerline{Problem Set 3}

\vskip 0.25truein

\vpar
Issued: September 24, 1987


\vpar
Due: in recitation on October 7, 1987 {\it for all sections}


\vpar
Reading assignment:  Chapter 2, Section 2.1, 2.2

\chapter{1. Homework exercises}

Write up and turn in the following exercises:

1. Exercise 2.18 from text

2. Exercise 2.19 from text

3. Exercise 2.20 from text

\chapter{ 2. Lecture Notes:  The Square-Limit Language}

The laboratory assignment for this week begins on page
8 of this handout.  For the assignment,
you are to implement a variation of Peter Henderson's ``Escher
Square-Limit'' language, discussed in lecture on
September 29, 1987.  This handout begins with a listing of the
procedures that implement Henderson's language.  The explanation given
here is very minimal, since this will be covered in lecture.

%%revised version for gobbling into Tex files

The Square-Limit Language is a simple graphics language is due to Peter
Henderson, of Oxford University.  One of the key features of 
the implementation is that it represents figures as procedures.

In particular, a ``picture'' is defined to be a procedure that takes a
rectangle as
input and causes something to be drawn, scaled to fit in the
rectangle.  A rectangle will be represented by three vectors: an
origin, a ``horizontal'' vector, and a ``vertical'' vector.   The origin \v{o}
will be represented as a vector whose coordinates give the coordinates
of the origin with respect to some external coordinate system, such as
the graphics screen coordinates.  The ``horizontal'' vector (call it
\v{h}) and the ``vertical'' vector (call it \v{v}) give the offsets of
the sides of the rectangle from the origin.  As a consequence, any
rectangle defines a linear transformation coordinate map by
mapping the point (1,0) in its coordinate frame into the point specified
by the end of the \v{horizontal} vector, offset from the origin, and by
mapping the point (0,1) in its coordinate frame into the point specified
by the end of the \v{vertical} vector, offset from the origin.

In algebraic terms, a general point $(x,y)$
gets mapped to the vector

\math{\v{o} + x*\v{h} + y*\v{v}}

where remember that \v{o}, \v{h}, and \v{v} are vectors, and $x$ and $y$
are scalars.

\vpar
We represent a rectangle by a list of three vectors: the location of the
origin of the rectangle, the vector from that origin to the end of the
``horizontal'' axis, and the vector to the end of the ``vertical'' axis.

\beginlisp
(define make-rect list)             ;; origin-vect H-vect V-vect
(define origin first)
(define horiz second)
(define vert third)
\endlisp
        
Using this map, points with coordinates between 0 and 1 end up inside
the rectangle.  Here is a procedure that takes a rectangle as input
and returns the corresponding coordinate transformation, which is itself
a procedure: 

\beginlisp
(define (coord-map rect)
  (lambda (point)
    (+vect
     (+vect (scale (xcor point)
                   (horiz rect))
            (scale (ycor point)
                   (vert rect)))
     (origin rect))))

\endlisp

The contents of a picture are defined by a
collection of line segments.
Segments are
represented as pairs of points, and
points (vectors) are represented as pairs of numbers:

\beginlisp
(define make-vector cons)
(define xcor car)
(define ycor cdr)
\pbrk
(define make-segment cons)
(define seg-start car)
(define seg-end cdr)
\endlisp


Given a rectangle as input, the picture
draws the image of each line segment inside the rectangle.  Here is a
procedure that constructs the picture, given a list of segments:
\beginlisp
(define (make-picture seglist)
  (lambda (rect)
    (for-each
     (lambda (segment)
       (drawline ((coord-map rect) (seg-start segment))
                 ((coord-map rect) (seg-end segment))))
     seglist)))

\pbrk
(define empty-picture (make-picture '()))
\endlisp

{\it Make-picture} uses a higher-order procedure {\it for-each}, which applies
a given procedure to each item in a given list:

\beginlisp
(define (for-each proc list)
  (cond ((null? list) "done")
        (else (proc (car list))
              (for-each proc (cdr list)))))
\endlisp

\section{Rotation}

To rotate a picture 90 degrees counterclockwise, we need only draw the
picture with respect to a new rectangle:

\beginfigure
\vskip 2truein
% V ---------------            --------------- H'=V
%   |             |            |             |
%   |         |   |            |   ---o      |
%   |  -------/   |    --->    |      |      |
%   |             |            |      |      |
%   ---------------            ---------------
% O               H           V'=-H          O'=O+H
\endfigure

\beginlisp
(define (rotate90 pict)
  (lambda (rect)
    (pict (make-rect
           (+vect (origin rect)          ;O'
                  (horiz rect))          
           (vert rect)                   ;H'
           (scale -1 (horiz rect))))))   ;V'
\pbrk
(define (repeated proc n)
  (lambda (thing)
    (if (= n 0)
        thing
        ((repeated proc (-1+ n)) (proc thing)))))
\pbrk
(define rotate180 (repeated rotate90 2))
(define rotate270 (repeated rotate90 3))
\endlisp

Horizontal flip is also drawing with respect to a new rectangle:
\beginfigure
\vskip 2truein
% V ---------------            --------------- V'=V
%   |             |            |             |
%   |         |   |            |  |          |
%   |  -------/   |    --->    |  \-------   |
%   |             |            |             |
%   ---------------            ---------------
% O               H           H'=-H          O'=O+H
\endfigure

\beginlisp
(define (flip pict)
  (lambda (rect)
    (pict (make-rect (+vect (origin rect) (horiz rect))
                     (scale -1 (horiz rect))
                     (vert rect)))))
\endlisp

\section{Means of combining pictures}

Since pictures are specified by procedures, we can create higher order
procedures that combine simple pictures together in various ways.  For
example, the {\it together} operation takes
two
pictures and combines them into a single picture, lying superimposed.
\beginlisp
(define (together pict1 pict2)
  (lambda (rect)
    (pict1 rect)
    (pict2 rect)))
\endlisp
The {\it beside} operation takes two pictures and
scales them according to some relative width to fit in a single rectangle. 
{\it Beside} takes as input, the two pictures, plus a variable {\it
a} which specifies the  percentage of horizontal devoted to the
first picture.

\beginlisp
(define (beside pict1 pict2 a)
  (lambda (rect)
    (pict1 (make-rect
            (origin rect)
            (scale a (horiz rect))
            (vert rect)))
    (pict2 (make-rect
            (+vect (origin rect)
                   (scale a (horiz rect)))
            (scale (- 1 a) (horiz rect))
            (vert rect)))))
\endlisp

{\it Above} is defined in terms of {\it beside}
\beginlisp
(define (above pict1 pict2 a)
  (rotate270 (beside (rotate90 pict1)
                     (rotate90 pict2)
                     a)))
\endlisp

Here are some operations defined in terms of the basic ones above:
\beginlisp
(define (4pict pict1 rot1 pict2 rot2 pict3 rot3 pict4 rot4)
  (beside (above ((repeated rotate90 rot1) pict1)
                 ((repeated rotate90 rot2) pict2)
                 .5)
          (above ((repeated rotate90 rot3) pict3)
                 ((repeated rotate90 rot4) pict4)
                 .5)
          .5))
\pbrk
(define (4same pict rot1 rot2 rot3 rot4)
  (4pict pict rot1 pict rot2 pict rot3 pict rot4))
\pbrk
(define (up-push pict n)
  (if (= n 0)
      pict
      (above (up-push pict (-1+ n))
             pict
             .25)))
\pbrk
(define (right-push pict n)
  (if (= n 0)
      pict
      (beside pict (right-push pict (-1+ n)) .75)))

\pbrk
(define (corner-push pict n)
  (if (= n 0)
      pict
      (above
       (beside (up-push pict n)
               (corner-push pict (-1+ n))
               .75)
       (beside
        pict
        (right-push pict (-1+ n))
        .75)
       .25)))
\pbrk
(define (square-limit pict n)
  (4same (corner-push pict n) 1 2 0 3))
\endlisp

\section{Rectangles and primitive pictures}

The rectangle that defines the original screen of the Chipmunk is
defined as:
\beginlisp
(define screen (make-rect (make-vector -190 -190) 
                          (make-vector 380 0)
                          (make-vector 0 380)))
\endlisp

A useful test picture is one that draws the ouline of its given
rectangle: 
\beginlisp
(define outline-picture
  (let ((v1 (make-vector 0 0))
        (v2 (make-vector 1 0))
        (v3 (make-vector 1 1))
        (v4 (make-vector 0 1)))
    (make-picture (list (make-segment v1 v2)
                        (make-segment v2 v3)
                        (make-segment v3 v4)
                        (make-segment v4 v1)))))
\endlisp
With the above, one could, for example, cause the outline rectangle to
be drawn on the full (square) screen by:
\beginlisp
(outline-picture screen)
\endlisp
The following procedure, which applies its argument to the screen after
clearing the graphics screen, will save some typing.
\beginlisp
(define (draw pict)
  (clear-graphics)
  (pict screen))
\endlisp
The definitions below present a few simple (and not very interesting)
patterns.  To gain a better understanding of the square-limit language,
you can play with them, using various combinations of the
operators, and define new primitives.
All the code in this description of the square-limit language may be
loaded by using {\bf load problem set} and specifying {\it problem set number}
as {\it squares}.  Note that this description (and code and suggestions)
are for your edification only.  There is nothing to hand in related to
the square-limit language.
\beginlisp
(define wedge (make-picture
               (list (make-segment (make-vector 0 0)
                                   (make-vector .5 .5))
                     (make-segment (make-vector .5 .5)
                                   (make-vector 1 0)))))

\pbrk
(define line (make-picture
              (list (make-segment (make-vector 0 .5)
                                  (make-vector 1 .5)))))
\pbrk
(define star (make-picture
              (list (make-segment (make-vector 0 .5)
                                  (make-vector .5 0))
                    (make-segment (make-vector .5 0)
                                  (make-vector 1 .5))
                    (make-segment (make-vector 1 .5)
                                  (make-vector .5 1))
                    (make-segment (make-vector .5 1)
                                  (make-vector 0 .5)))))
\endlisp

\chapter{3. Laboratory Assignment: A Drawing Language Based on Circles}

For this assignment, you are to implement a language similar to
Henderson's, except based on circles rather than squares.  We begin
by describing the implementation, and listing the procedures that have
been installed on the Chipmunk system for you to work with.

The implementation of the language is very similar to the one
described above.

\section{Representation of a Circle}

A circle is identified by three pieces of information:  its center,
its radius and an angle which is the offset of its zero-degree axis
with respect to the x-axis in the external coordinate system.  The
center is represented by a vector whose coordinates give the coordinates
of the circle's center with respect to the external coordinate system.
The radius is simply represented by a number which is the length of the
circle's radius measured with respect to the external coordinate system.
Finally, the angle is represented by a number in radians.

\beginlisp
(define make-circle list)
(define center car)
(define radius cadr)
(define theta caddr)
\pbrk
(awline start end)
  (position-pen (xcor start) (ycor start))
  (draw-line-to (xcor end) (ycor end)))
\endlisp

The following procedure needs a little bit of thought to understand.  It takes a circle as
argument, and returns a procedure.  This returned procedure takes a point, and calculates
the position of the point relative to the circle's center, radius and
angle. It returns the position of the point in vector form with respect
to the external coordinate system.

\beginlisp
(define (coord-map circle)
  (lambda (point)
    (let ((angle (theta circle))
          (r (radius circle)))
      (+vect
        (center circle)
        (make-vector (* r (cos (+ angle point)))
                     (* r (sin (+ angle point))))))))
\endlisp

Finally, we define a standard {\tt screen-circle}, which is the
biggest circle one could draw in the center of a Chipmunk's
screen:

\beginlisp
(define screen-circle (make-circle (make-vector 0 0) 190 0))
\endlisp

\section{Pictures}

As in the square-limit language, a picture is a procedure, this time a
procedure on circles, which are drawn inside the given circle.
{\tt make-picture} constructs a picture from a list of chords:
\beginlisp
(define (make-picture chordlist)
  (lambda (circle)
    (for-each
     (lambda (chord)
       (drawline ((coord-map circle) (chord-start chord))
                 ((coord-map circle) (chord-end chord))))
     chordlist)))
\endlisp

The {\tt for-each} procedure is a very useful higher-order
procedure.  It's code is listed in the appendix.
Also, as in the square-limit language, we include the following
procedure, which clears the screen and causes a designated picture to
be drawn in the standard {\tt screen-circle}:

\beginlisp
(define (draw pict)
  (clear-graphics)
  (pict screen-circle))
\endlisp

We will also define some simple pictures to use as drawing elements:
a picture that draws a cross, a square, a triangle and an arrow inside
a circle.  We begin by first picking some points on a circle, then
connecting them in order to form the figures mentioned.

\beginlisp
(define pi 3.1415927)
(define 2pi (* 2 pi))
(define quarter-pi (/ pi 4))
(define p1 quarter-pi)
(define p2 (* 3 quarter-pi))
(define p3 (* 5 quarter-pi))
(define p4 (* 7 quarter-pi))
(define p5 (* 6 quarter-pi))
(define p6 (* 2 quarter-pi))
\pbrk
(define cross (make-picture (list (make-chord p1 p3)
                                  (make-chord p2 p4))))
\pbrk
(define square (make-picture (list (make-chord p1 p2)
                                   (make-chord p2 p3)
                                   (make-chord p3 p4)
                                   (make-chord p4 p1))))
\pbrk
(define triangle (make-picture (list (make-chord p1 p2)
                                     (make-chord p1 p5)
                                     (make-chord p2 p5))))
\pbrk
(define arrow (make-picture (list (make-chord p1 pi)
                                  (make-chord p4 pi))))
\endlisp

\section{Means of combination}

We define a means of combination, called {\tt split}, which takes
as arguments two pictures and a {\tt ratio} between 0 and 1, and returns
a procedure.  When the returned procedure is given 
a circle as argument, it procedure first splits the
circle by dividing the radius of the circle as specified by the
{\tt ratio}.  Then it draws one picture in each of the two subcircles,
as shown in figure 1.

\beginfigure
\vskip 4in
\endfigure

The procedure {\tt split} must compute the new centers and radii of
each of the two component circles.

\beginlisp
(define (split pict1 pict2 ratio)
  (lambda (circle)
    (let ((r (radius circle))
          (t (theta circle))
          (o (center circle)))
      (define r1 (* ratio r))
      (define r2 (- r r1))
      (define c1 (+vect o (scale (- 1 ratio)
                                 (scale r (make-vector (cos t) (sin t))))))  
      (define c2 (+vect o (scale (- 0 ratio)
                                 (scale r (make-vector (cos t) (sin t))))))
      (pict1 (make-circle c1 r1 t))
      (pict2 (make-circle c2 r2 t)))))
\endlisp

Notice that the split is performed along the zero-axis of the circle,
not the x-axis of the external coordinate system.  The subcircles
inherit the zero-axis offset from the big circle.

\section{To do in lab}

Begin by installing the code for problem set 3 on your floppy disk,
using the {\tt load-problem-set} operation documented in the Chipmunk
manual.  The file to be loaded contains all of the code listed above
in section 2.  You will not need to edit
or modify any of it.  The ``modifications file'' for problem set 3 that
will be installed on your disk is an empty file, since for this
problem set you will be writing new procedures, rather than modifying
procedures that we supply.  You will also need to bring with you the
instruction sheet on "How to Dump Chipmunk Graphics" which we gave out
with problem set 1, since you will be asked to hand in some pictures.

\section{Part 1}

Draw some pictures to test the procedures.  When you are more
comfortable with the system, define a picture called {\tt polygon},
which takes one argument {\tt n} and returns a picture of an $n$-sided
regular polygon.  Execute your procedure by drawing an octagon in
the {\tt screen-circle}.

Hand in a listing of the procedure and a picture of the octagon.

\section{Part 2}

It would be nice to see the frame circle together with the pictures
in it.  Make use of the {\tt polygon} procedure to define a picture
called {\tt outline-picture}, which draws a very close approximation
of the frame circle.  (Hint: this one is very easy.)

\section{Part 3}

Try using {\tt split} to make various combinations of the predefined
elementary pictures.  When you have a good feeling of how this 
procedure works, use it to define an operation called {\tt push} which
is analogous to {\tt right-push} of the square-limit language.
(actually, {\tt split} as defined would perform a ``left-push''
-- it would
squeeze everything towards the left.  Since we do not intend to have
you modify the split procedure, don't worry about the direction of
the push.)  Test your procedure by trying it out on some of the simple
figures provided.

Hand in a listing of your procedure.

\section{Part 4}

For this part, you are to define a {\tt rotate} operator on pictures. 
The rotation of a picture draws the picture in the specified circle, but
``rotated'' by 45 degrees.  See figure 2 below.

\beginfigure 
\vskip 3in
\endfigure

Here is a segment of the code to get you started in the right direction:
\beginlisp
(define (rotate pict)
  (lambda (circle)
    <??>))
\endlisp
(Hint:  think about the frame of the picture rather than the picture
itself.)
Test {\tt rotate} on some simple pictures.  Note that
{\tt outline-picture} and an octagon won't look any different when
rotated, so you should use {\tt square} or {\tt triangle} to test your
procedure.

We can build a higher order procedure called {\tt rotate-n}, which
rotates a picture $n$ times by 45 degrees.  We ask you to use the
{\tt repeated} procedure provided in the code to implement this
application.  You should know how {\tt repeated} works since it
is discussed in problem set 2.
Test out your procedure to make sure it works.

Hand in a listing of your {\tt rotate} and {\tt rotate-n} procedures.

\section{Part 5}

We can make our rotation procedures more useful by making them able
to rotate a picture by any arbitrary angle, as specified by the user.
First, modify your {\tt rotate} so that it takes an extra argument
{\tt angle} and rotates the picture by that angle.  Call this new
rotation procedure {\tt rotate2}.  Test it out to make
sure it works.  At this point, you'll realize that your old {\tt rotate-n}
doesn't work anymore because {\tt repeated} only works on procedures with
only 1 argument.   Write a new repeated
procedure, called {\tt repeated2}, which works for procedures taking
two arguments.  (Hint:  Make use of the fact that
one of the arguments is an invariant in the process of the repetition -
the angle.  You also have to write a new {\tt compose} procedure, called
{\tt compose2}.  If both arguments are varying, it would be much harder
to write.)  Finally, mode generalized by
modifying it to be {\tt n-fold}.  Basically, {\tt n-fold} takes
a picture and another argument {\tt n}, and returns a picture
which is the superposition of n replicates of the original
picture, rotated n times.  For aesthetic purpose, this
procedure should also be able to calculate the angle of
rotation such that after $n$ rotations would cover 360 degrees.
This would spread the rotated pictures evenly in the circle.

Hand in a listing of your procedure and a picture of a
10-fold arrow.

\section{Part 8}

\beginfigure
\vskip 4in
\endfigure

Figure 3 illustrates a means of combination called
{\tt 4split}, which takes as its arguments four pictures and a
ratio.  It puts the 4 pictures into the four smaller circles
as shown in the figure.  For this part, you are to implement
{\tt 4split}.

This procedure can be rather tricky, especially if you are not used to
the geometry of the circle-language yet, so think about it carefully
before you start coding.  A good way to proceed is to think about the
relationship among the centers of the 4 subcircles.

Hand in a listing of your procedure and one of your favorite pictures
drawn by this procedure.

\section{Part 9}

The following {\tt equal-split} procedure uses {\tt 4-split} to replicate
a single picture 4 times, splitting the radius of the original circle
by the ratio 0.4.

\beginlisp
(define (equal-split pict)
  (4-split pict pict pict pict 0.4))
\endlisp

We could imagine a generalization of {\tt equal-split}, which uses the
same picture 4 times, but with each of the smaller circles rotated
by applying {\tt rotate} as many times as specified.  One way to do
this is to define a procedure similar to {\tt equal-split}, but which 
takes as arguments a picture, and also 4 numbers - each of which is
the number of rotations for each smaller circle, and an angle, which
is the angle of rotation.  

An alternative method is to use a higher-order procedure, which, given
the four rotation numbers and the angle, will generate a procedure that
takes a picture as argument.  In other words, if we call this higher-
order procedure {\tt make-splitter}, then writing

\beginlisp
(define equal-split (make-splitter 0 0 0 0 0))
\endlisp

should be an equivalent way to define {\tt equal-split}, and

\beginlisp
(define alternate-equal-split (make-splitter 0 1 2 3 (/ pi 2)))
\endlisp

should produce a procedure that is like {\tt equal-split}, but which
rotates the second, third and fourth small picture before drawing
them.  

For this part, you are to implement the {\tt make-splitter} procedure.
As a hint, note that {\tt make-splitter} will have the following form:

\beginlisp
(define (make-splitter r1 r2 r3 r4 angle)
  (lambda (pict)
    ...))
\endlisp

As a test, try drawing
\beginlisp
(define mesh ((make-splitter 0 2 4 6 quarter-pi) triangle))
\endlisp

Hand in a listing of {\tt make-splitter}.

\section{Part 10}

Suppose we use {\tt 4-split} recursively: split a picture which is itself
a split, and so on.  Here is an extended version of the
{\tt equal-split} procedure given above, which performs this splitting
to a specified number of levels:
\beginlisp
(define (rec-equal-split pict levels)
  (if (= levels 0)
      pict
      (let ((p (rec-equal-split pict (-1+ levels))))
        (4-split p p p p 0.4))))
\endlisp

Now suppose we want to generalize this procedure as in part 9, that is,
to have a higher-order procedure that will create things like
{\tt rec-equal-split}, except where the splitting ratios of the sides
can be other than 0.4.  We want a procedure
{\tt make-rec-splitter}, such that {\tt rec-equal-split} could be defined as
\beginlisp
(define rec-equal-split (make-rec-splitter 0.4))
\endlisp

Hand in a listing of your procedure and a picture drawn by using squares,
0.4 as the value for the {\tt r} parameter and 3 as the value for the
{\tt level} parameter.

\section{Part 11}

Suppose we decide to change the representation of chords so that
{\tt make-chord} is now defined as:
\beginlisp
(define make-chord list)
\endlisp

What other definitions in the circle-drawing system must
be changed as a consequence of making this change in the representation
for chords?


\end
