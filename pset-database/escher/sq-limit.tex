%%revised version for gobbling into Tex files
% Copyright (c) 1990 Massachusetts Institute of Technology
% 
% This material was developed by the Scheme project at the Massachusetts
% Institute of Technology, Department of Electrical Engineering and
% Computer Science.  Permission to copy this material, to redistribute
% it, and to use it for any non-commercial purpose is granted, subject
% to the following restrictions and understandings.
% 
% 1. Any copy made of this material must include this copyright notice
% in full.
% 
% 2. Users of this material agree to make their best efforts (a) to
% return to the MIT Scheme project any improvements or extensions that
% they make, so that these may be included in future releases; and (b)
% to inform MIT of noteworthy uses of this material.
% 
% 3. All materials developed as a consequence of the use of this
% material shall duly acknowledge such use, in accordance with the usual
% standards of acknowledging credit in academic research.
% 
% 4. MIT has made no warrantee or representation that this material
% (including the operation of software contained therein) will be
% error-free, and MIT is under no obligation to provide any services, by
% way of maintenance, update, or otherwise.
% 
% 5. In conjunction with products arising from the use of this material,
% there shall be no use of the name of the Massachusetts Institute of
% Technology nor of any adaptation thereof in any advertising,
% promotional, or sales literature without prior written consent from
% MIT in each case. 



The Square-Limit Language is a simple graphics language is due to Peter
Henderson, of Oxford University.  One of the key features of 
the implementation is that it represents figures as procedures.

In particular, a ``picture'' is defined to be a procedure that takes a
rectangle as
input and causes something to be drawn, scaled to fit in the
rectangle.  A rectangle will be represented by three vectors: an
origin, a ``horizontal'' vector, and a ``vertical'' vector.   The origin \v{o}
will be represented as a vector whose coordinates give the coordinates
of the origin with respect to some external coordinate system, such as
the graphics screen coordinates.  The ``horizontal'' vector (call it
\v{h}) and the ``vertical'' vector (call it \v{v}) give the offsets of
the sides of the rectangle from the origin.  As a consequence, any
rectangle defines a linear transformation coordinate map by
mapping the point (1,0) in its coordinate frame into the point specified
by the end of the \v{horizontal} vector, offset from the origin, and by
mapping the point (0,1) in its coordinate frame into the point specified
by the end of the \v{vertical} vector, offset from the origin.

In algebraic terms, a general point $(x,y)$
gets mapped to the vector

\math{\v{o} + x*\v{h} + y*\v{v}}

where remember that \v{o}, \v{h}, and \v{v} are vectors, and $x$ and $y$
are scalars.

\vpar
We represent a rectangle by a list of three vectors: the location of the
origin of the rectangle, the vector from that origin to the end of the
``horizontal'' axis, and the vector to the end of the ``vertical'' axis.

\beginlisp
(define make-rect list)             ;; origin-vect H-vect V-vect
(define origin first)
(define horiz second)
(define vert third)
\endlisp
	
Using this map, points with coordinates between 0 and 1 end up inside
the rectangle.  Here is a procedure that takes a rectangle as input
and returns the corresponding coordinate transformation, which is itself
a procedure: 

\beginlisp
(define (coord-map rect)
  (lambda (point)
    (+vect
     (+vect (scale (xcor point)
                   (horiz rect))
            (scale (ycor point)
                   (vert rect)))
     (origin rect))))

\endlisp

The contents of a picture are defined by a
collection of line segments.
Segments are
represented as pairs of points, and
points (vectors) are represented as pairs of numbers:

\beginlisp
(define make-vector cons)
(define xcor car)
(define ycor cdr)
\pbrk
(define make-segment cons)
(define seg-start car)
(define seg-end cdr)
\endlisp


Given a rectangle as input, the picture
draws the image of each line segment inside the rectangle.  Here is a
procedure that constructs the picture, given a list of segments:
\beginlisp
(define (make-picture seglist)
  (lambda (rect)
    (for-each
     (lambda (segment)
       (drawline ((coord-map rect) (seg-start segment))
                 ((coord-map rect) (seg-end segment))))
     seglist)))

\pbrk
(define empty-picture (make-picture '()))
\endlisp

{\it Make-picture} uses a higher-order procedure {\it for-each}, which applies
a given procedure to each item in a given list:

\beginlisp
(define (for-each proc list)
  (cond ((null? list) "done")
        (else (proc (car list))
              (for-each proc (cdr list)))))
\endlisp

\section{Rotation}

To rotate a picture 90 degrees counterclockwise, we need only draw the
picture with respect to a new rectangle:

\beginfigure
\vskip 2truein
% V ---------------            --------------- H'=V
%   |             |            |             |
%   |         |   |            |   ---o      |
%   |  -------/   |    --->    |      |      |
%   |             |            |      |      |
%   ---------------            ---------------
% O               H           V'=-H          O'=O+H
\endfigure

\beginlisp
(define (rotate90 pict)
  (lambda (rect)
    (pict (make-rect
           (+vect (origin rect)          ;O'
                  (horiz rect))          
           (vert rect)                   ;H'
           (scale -1 (horiz rect))))))   ;V'
\pbrk
(define (repeated proc n)
  (lambda (thing)
    (if (= n 0)
        thing
        ((repeated proc (-1+ n)) (proc thing)))))
\pbrk
(define rotate180 (repeated rotate90 2))
(define rotate270 (repeated rotate90 3))
\endlisp

Horizontal flip is also drawing with respect to a new rectangle:
\beginfigure
\vskip 2truein
% V ---------------            --------------- V'=V
%   |             |            |             |
%   |         |   |            |  |          |
%   |  -------/   |    --->    |  \-------   |
%   |             |            |             |
%   ---------------            ---------------
% O               H           H'=-H          O'=O+H
\endfigure

\beginlisp
(define (flip pict)
  (lambda (rect)
    (pict (make-rect (+vect (origin rect) (horiz rect))
                     (scale -1 (horiz rect))
                     (vert rect)))))
\endlisp

\section{Means of combining pictures}

Since pictures are specified by procedures, we can create higher order
procedures that combine simple pictures together in various ways.  For
example, the {\it together} operation takes
two
pictures and combines them into a single picture, lying superimposed.
\beginlisp
(define (together pict1 pict2)
  (lambda (rect)
    (pict1 rect)
    (pict2 rect)))
\endlisp
The {\it beside} operation takes two pictures and
scales them according to some relative width to fit in a single rectangle. 
{\it Beside} takes as input, the two pictures, plus a variable {\it
a} which specifies the  percentage of horizontal devoted to the
first picture.

\beginlisp
(define (beside pict1 pict2 a)
  (lambda (rect)
    (pict1 (make-rect
            (origin rect)
            (scale a (horiz rect))
            (vert rect)))
    (pict2 (make-rect
            (+vect (origin rect)
                   (scale a (horiz rect)))
            (scale (- 1 a) (horiz rect))
            (vert rect)))))
\endlisp

{\it Above} is defined in terms of {\it beside}
\beginlisp
(define (above pict1 pict2 a)
  (rotate270 (beside (rotate90 pict1)
                     (rotate90 pict2)
                     a)))
\endlisp

Here are some operations defined in terms of the basic ones above:
\beginlisp
(define (4pict pict1 rot1 pict2 rot2 pict3 rot3 pict4 rot4)
  (beside (above ((repeated rotate90 rot1) pict1)
                 ((repeated rotate90 rot2) pict2)
                 .5)
          (above ((repeated rotate90 rot3) pict3)
                 ((repeated rotate90 rot4) pict4)
                 .5)
          .5))
\pbrk
(define (4same pict rot1 rot2 rot3 rot4)
  (4pict pict rot1 pict rot2 pict rot3 pict rot4))
\pbrk
(define (up-push pict n)
  (if (= n 0)
      pict
      (above (up-push pict (-1+ n))
             pict
             .25)))
\pbrk
(define (right-push pict n)
  (if (= n 0)
      pict
      (beside pict (right-push pict (-1+ n)) .75)))

\pbrk
(define (corner-push pict n)
  (if (= n 0)
      pict
      (above
       (beside (up-push pict n)
               (corner-push pict (-1+ n))
               .75)
       (beside
        pict
        (right-push pict (-1+ n))
        .75)
       .25)))
\pbrk
(define (square-limit pict n)
  (4same (corner-push pict n) 1 2 0 3))
\endlisp

\section{Rectangles and primitive pictures}

The rectangle that defines the original screen of the Chipmunk is
defined as:
\beginlisp
(define screen (make-rect (make-vector -190 -190) 
                          (make-vector 380 0)
                          (make-vector 0 380)))
\endlisp

A useful test picture is one that draws the ouline of its given
rectangle: 
\beginlisp
(define outline-picture
  (let ((v1 (make-vector 0 0))
        (v2 (make-vector 1 0))
        (v3 (make-vector 1 1))
        (v4 (make-vector 0 1)))
    (make-picture (list (make-segment v1 v2)
                        (make-segment v2 v3)
                        (make-segment v3 v4)
                        (make-segment v4 v1)))))
\endlisp
With the above, one could, for example, cause the outline rectangle to
be drawn on the full (square) screen by:
\beginlisp
(outline-picture screen)
\endlisp
The following procedure, which applies its argument to the screen after
clearing the graphics screen, will save some typing.
\beginlisp
(define (draw pict)
  (clear-graphics)
  (pict screen))
\endlisp
The definitions below present a few simple (and not very interesting)
patterns.  To gain a better understanding of the square-limit language,
you can play with them, using various combinations of the
operators, and define new primitives.
All the code in this description of the square-limit language may be
loaded by using {\bf load problem set} and specifying {\it problem set number}
as {\it squares}.  Note that this description (and code and suggestions)
are for your edification only.  There is nothing to hand in related to
the square-limit language.
\beginlisp
(define wedge (make-picture
               (list (make-segment (make-vector 0 0)
                                   (make-vector .5 .5))
                     (make-segment (make-vector .5 .5)
                                   (make-vector 1 0)))))

\pbrk
(define line (make-picture
              (list (make-segment (make-vector 0 .5)
                                  (make-vector 1 .5)))))
\pbrk
(define star (make-picture
              (list (make-segment (make-vector 0 .5)
                                  (make-vector .5 0))
                    (make-segment (make-vector .5 0)
                                  (make-vector 1 .5))
                    (make-segment (make-vector 1 .5)
                                  (make-vector .5 1))
                    (make-segment (make-vector .5 1)
                                  (make-vector 0 .5)))))
\endlisp

